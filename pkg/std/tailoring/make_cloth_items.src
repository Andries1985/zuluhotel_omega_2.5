/////////////////////////////////////////////////////////////////////////////
//
//	Tailoring: Original Code by Racalac
//	Updated: 8/25/1999	 By: Sentack
//	TODO: Move all the UOBJ objects to "include/objtype.inc"
//
//	NOTES: Lines 129 and 196 are optional statments that are designed to
//		replace lines 127 and 194.	They are used to remove a random amount of
//		cloth from the players inventory instead of just one unit of cloth
//		as does the OSI way of dealing with failer.	Sometimes you lose a little
//		while other times you lose a lot.	But never more then the amount of
//		cloth the object original was going to take.
/////////////////////////////////////////////////////////////////////////////

use uo;
use cfgfile;
use os;
use math;
use basicio;
use polsys;

include ":gumps:yesno";
include "include/objtype";
include "include/client";
include "include/string";
include "include/classes";
include "include/itemutil";
include "include/random";
include "util/repair";
include "include/autoloop";
include "include/attributes";
include "include/difficulty";

const SCISSORS_SOUND    := 0x0249;
const ID_HIDES          := 1;
const ID_CLOTH          := 2;
const DIVIDER           := 3;
const APPEAR_AT         := -10;
const POINTS_MULTIPLIER := 8;
const FAME_BONUS        := 10;

var tailor_cfg          := ReadConfigFile( "tailoring" );
var tailor_items        := ReadConfigFile( ":tailoring:itemdesc" );
var resource_diff       := 0;
var resource_qual       := 1;
var resource_name := 0;

program use_sewing_kit( character, sewing_kit )

	if( !ReserveItem( sewing_kit ) )
		return;
	endif

	var item;

	SendSysmessage( character, "What would you like to use that on?" );
	var use_on := Target( character, TGTOPT_CHECK_LOS );

	if( !use_on )
		return;
	endif

	if( !Accessible( character, use_on ) )
		SendSysMessage( character, "You can't reach that." );
		return;
	endif

	if( !ReserveItem(use_on) )
		return;
	endif

	if( !IsInContainer( character.backpack, use_on ) )
		SendSysmessage( character, "That item has to be in your backpack." );
		return;
	endif

	if(!use_on.hp && use_on.quality && tailor_items[use_on.objtype])
		use_on.hp := 1;
	endif

	if( IsHat( use_on ) and use_on.hp == use_on.maxhp )
		FortifyHat( character, use_on );

	elseif( IsHide( use_on.objtype ) )
		item := MakeAndProcessMenu( character, use_on, ID_HIDES );

	elseif( use_on.objtype == UOBJ_MUTLI_CLOTH )
		item := MakeAndProcessMenu( character, use_on, ID_CLOTH );

	elseif( use_on.hp and use_on.quality and tailor_cfg[use_on.graphic] )
		repair_item( character, use_on, SKILLID_TAILORING );

	else
		SendSysMessage( character, "You don't know how to use those items together." );
	endif

	
	item := item.objtype;
	if( !item )
		return;
	endif

	TryToMakeItem( character, use_on, item );

return;
endprogram


function MakeAndProcessMenu( character, use_on, id )

	resource_diff := tailor_cfg[use_on.objtype].Difficulty;

	var skill := GetEffectiveSkill( character, SKILLID_TAILORING );
	if( skill < resource_diff )
		sendDiff( character , resource_diff );
		SendSysmessage( character, "You aren't skilled enough to make anything with this yet." );
		return;
	endif

	resource_name := tailor_cfg[use_on.objtype].Name;
	resource_qual := tailor_cfg[use_on.objtype].Quality;

	var elemkeys := GetConfigStringKeys( tailor_cfg );
	var can_make;
	var choice;

	var menu := CreateMenu( "What would you like to make?" );

	case( id )

		ID_HIDES:
			AddMenuItem( menu, 0x13CC, "Leather Armors" );
			AddMenuItem( menu, 0x13DB, "Studded Leather Armors" );
			AddMenuItem( menu, 0x1C02, "Females Armors" );
			AddMenuItem( menu, 0x1711, "Shoes" );
			AddMenuItem( menu, 0x141b, "Misc" );
			AddMenuItem( menu, 0x0E76, "Containers" );
			break;

		ID_CLOTH:
			AddMenuItem( menu, 0x1517, "Shirts" );
			AddMenuItem( menu, 0x1539, "Pants" );
			AddMenuItem( menu, 0x1713, "Caps" );
			AddMenuItem( menu, 0x153D, "Misc" );
			AddMenuItem( menu, 0x0AD1, "Carpet" );
			break;

	endcase

	choice := SelectMenuItem2( character, menu ).index;
	if( !choice )
		return;
	endif

	choice := (id * 10) + choice;

	menu := CreateMenu( "What would you like to make?" );

	foreach key in elemkeys
		if( tailor_cfg[key].Type == choice )
			if( CanMake( character, key, use_on ) )
				AddMenuItem( menu, Cint(key), tailor_cfg[key].Name );
				can_make := 1;
			endif
		endif
	endforeach

	if( can_make )
		return SelectMenuItem2( character, menu );
	else
		SendSysmessage( character, "You can't make anything of this category." );
		return;
	endif

endfunction

function CanMake( who, key, resource )

	var diff := tailor_cfg[key].Skill + Cint( resource_diff / DIVIDER );
	if( diff < 1 )
		diff := 1;
	elseif( diff > 150 )
		diff := 150;
	endif

	if( GetEffectiveSkill( who, SKILLID_TAILORING ) >= ( diff + APPEAR_AT ) )
		if( resource.amount >= tailor_cfg[key].Material )
			return 1;
		endif
	endif

	return 0;

endfunction

function TryToMakeItem( character, resource, to_make_objtype )

	var material;
	var itemname;

	var element := FindConfigElem( tailor_cfg, to_make_objtype );
	if( element )
		material	:= element.Material;
	else
		return;
	endif

	var skill := element.Skill + CInt( resource_diff / DIVIDER );

	if( skill < 1 )
		skill := 1;
	elseif( skill > 150 )
		skill := 150;
	endif

	var points	:= CInt( (skill + element.Material) * POINTS_MULTIPLIER );

	//Require half resources during Crafting PH
	if(GetGlobalProperty("PHC") || GetObjProperty(character, "#PPHC"))
		material := CInt(Ceil(material/2));
	endif
	
	var excep_ch := 10;
	var excep_diff	:= skill;

	if( GetObjProperty( character, CLASSEID_CRAFTER ) )
		excep_ch := Cint( excep_ch * ClasseBonus( character, CLASSEID_CRAFTER ) );
		excep_diff += 20;
	else
		excep_diff += 40;
	endif

	if( excep_diff < 90 )
		excep_diff := 90;
	endif

	AutoLoop_init( character );
	while( AutoLoop_more() && ( resource.amount >= material ) && NOT character.dead)

		sendDiff( character , skill );

		itemname := element.Name;

		_Play_Sound( character );

		if( CheckSkill( character, SKILLID_TAILORING, skill, points ) )

			var theitem;
			if( to_make_objtype == UOBJ_BANDAGE )
				material := resource.amount;
				theitem := CreateItemInBackpack( character, to_make_objtype, CInt( material / 4 ) );
			else
				theitem := CreateItemInBackpack( character, to_make_objtype );
			endif

			if( theitem )

				var cprops   := GetConfigStringArray( tailor_cfg[resource.objtype], "CProp" );

				if( resource_name )
					itemname := resource_name + " " + itemname;
				endif

				if( theitem.quality and resource_qual )
					theitem.quality := resource_qual;
				endif

				if( ( Random(100) < excep_ch ) && CheckSkill( character, SKILLID_TAILORING, excep_diff, 0 ) )

					//Made something Exceptional
					if( theitem.quality )

						var armslore := GetEffectiveSkill( character, SKILLID_ARMSLORE );
						var multiplier := 5 + Cint( armslore / 10 );

						if( GetObjProperty( character, CLASSEID_CRAFTER ) )
							multiplier := Cint( multiplier * ClasseBonus( character, CLASSEID_CRAFTER ) );
						endif

						multiplier := 100 + multiplier;
						var newquality := Cint(theitem.quality * multiplier);
						newquality := Cdbl(newquality);
						newquality := newquality / 100;
						theitem.quality := newquality;
					endif

					itemname := "Exceptional " + itemname;
					if(GetObjProperty(character, "ToggleBuildMark"))
						if( YesNo( character, "Add Builder Mark?" ) )
							SetObjProperty( theitem , "CraftedBy" , character.name );
						endif
					else
						SetObjProperty( theitem , "CraftedBy" , character.name );
					endif

					SendSysmessage( character, "You make an exceptional item and put it in your pack" );
					SetObjProperty( theitem, "IsExceptional", 1 );
					if( excep_diff >= 130 )
						SendSysmessage( character, "This masterpiece will make you famous around the world." );
						AwardFame( character, FAME_BONUS );
					elseif( GetObjProperty( character, CLASSEID_CRAFTER ) and excep_diff >= 110 )
						SendSysmessage( character, "This masterpiece will make you famous around the world." );
						AwardFame( character, FAME_BONUS );
					endif
				else
					//Made a normal item
					SendSysmessage( character, "You create the item and place it in your pack." );
				endif

				if( Len( cprops ) && ( theitem.equipscript == "::skilladvancerequip" ) )
					foreach cprop in cprops
						var splited := SplitWords( cprop );
						var prop_name := splited[1];
						var prop_mod  := Cint(splited[2]);
						if( prop_name and prop_mod )
							var presently := GetObjProperty( theitem, prop_name );
							if( !presently )
								presently := 0;
							endif

							if( presently and abs(prop_mod) > abs(presently) )
							// magic penalty works normally
								if (prop_name != "MagicPenalty")
									prop_mod := 0 - presently;
								endif
							endif
		
							var resulting := presently + prop_mod;
							if( resulting > 0 and prop_name == "DefaultDex" )
								resulting := 0;
							endif
							if( resulting || resulting < 0  )
								SetObjProperty( theitem, prop_name, resulting );
							elseif( presently )

							// Toadstool - 21/04/2012 - Due to a core bug with items (just armour that i know of)
							// getting back their basic props on server restart. Set deleted ones to 0
								SetObjProperty( theitem, prop_name, 0);
								//EraseObjProperty( theitem, prop_name );
							endif
						endif
					endforeach
				endif

				theitem.hp := theitem.maxhp;
				theitem.color := resource.color;
				SetObjProperty( theitem , "CraftedWith", resource.objtype );
				SubtractAmount( resource, material );
				SetObjProperty( theitem, "BaseName", itemname );
				SetName( theitem, itemname );
				IncRevision(theitem);
			else
				SubtractAmount( resource, material );
			//	AutoLoop_finish();
				SendSysmessage( character, "Your backpack is full." );
			//	return;
			endif
		else
			var tail := GetEffectiveSkill( character, SKILLID_TAILORING );
			var destroy := 75 - CInt( tail / 2 );
			if( GetObjProperty( character, CLASSEID_CRAFTER ) )
				destroy := Cint( destroy / ClasseBonus( character, CLASSEID_CRAFTER ) );
			endif

			if( destroy > 50 )
				destroy	:= 50;
			endif

			destroy	:= Cint( material * destroy / 100 );
			if( destroy < 1 )
				destroy	:= 1;
			endif
			
			SubtractAmount( resource, destroy );
			SendSysmessage( character, "You destroyed " + destroy + " " + GetresourceName( resource ) + "." );

		endif
	endwhile

	AutoLoop_finish();

endfunction


function GetresourceName( resource )

	var t_name := resource.desc;

	if( resource.amount > 1 )
		var i := len( t_name );
		var amt := resource.amount;
		var count := 2;
		while( Cint(amt / 10) )
			count := count + 1;
			amt := Cint( amt / 10 );
		endwhile

		t_name := t_name[count+1, i - count];
	endif

	return t_name;

endfunction


function _Play_Sound( character )

	PlaySoundEffect( character, SCISSORS_SOUND );
	Sleep( 1 );
	PlaySoundEffect( character, SCISSORS_SOUND );
	Sleep( 1 );

endfunction

function FortifyHat( who, hat )
	
	if (GetObjProperty(hat, "FortifiedWith"))
		SendSysMessage(who, "This hat is already fortified.");
		return 0;
	endif

	if (GetObjProperty(hat, "Entwined") || GetObjProperty(hat,"GuildUniform") )
		SendSysMessage(who, "You must unentwine the hat to do that.");
		return 0;
	endif
	
	SendSysMessage( who, "Fortify this hat with what?" );
	var helm := Target( who, TGTOPT_CHECK_LOS );
	if( !helm )
		return;
	endif

	if( !IsHelm(helm) )
		SendSysMessage( who, "That's not a helmet." );
		return;
	endif

	if( GetObjProperty( helm , "Cursed" ) )
		SendSysMessage( who, "That helmet seems to be cursed. Purify it first." );
		return;
	endif

	if( !ReserveItem(helm) )
		return 0;
	endif

	if( !IsInContainer( who.backpack, helm ) )
		SendSysmessage( who, "The helm has to be in your backpack." );
		return;
	endif

	// this part most probably isn't needed, but havnt tested the above. just incase :P
	if (GetObjProperty(helm, "Entwined") || GetObjProperty(helm,"GuildUniform") )
		SendSysMessage(who, "You must unentwine the helm to do that.");
		return 0;
	endif
	
	_Play_Sound( who );

	if( !CheckSkill( who, SKILLID_TAILORING, -1, 250 ) )
		SendSysMessage( who, "You fail and ruin both, the hat and the helm." );
		DestroyItem( hat );
		DestroyItem( helm );
		return;
	endif

	SetObjProperty(helm, "FortifiedWith", hat.desc);
	hat.quality := 1.8; 
	DupeItem( hat, helm );
	SetName( helm, "a fortified " + TruncateArticle(hat.desc) );
	helm.hp := helm.maxhp;
	DestroyItem( hat );
	IncRevision(helm);

endfunction


function IsHat( what )

	case( what.graphic )
		0x153F:
		0x1545:
		0x1546:
		0x1547:
		0x1548:
		0x1549:
		0x154a:
		0x154b:
		0x154c:
		0x141b:
		0x141c:
		0x1540:
		0x1543:
		0x1544:
		0x1713:
		0x1714:
		0x1715:
		0x1716:
		0x1717:
		0x1718:
		0x1719:
		0x171A:
		0x171B:
		0x171C:		return 1;

		default:	return 0;
	endcase

endfunction


function IsHelm( what )

	case (what.graphic)
		0x13BB:
		0x13C0:
		0x1408:
		0x1409:
		0x140A:
		0x140B:
		0x140C:
		0x140D:
		0x140E:
		0x140F:
		0x1412:
		0x1419:
		0x1451:
		0x1456:
		0x1DB9:
		0x1DBA:		return 1;
				break;

		default:	return 0;
	endcase

endfunction