use os;
use uo;

include "include/spelldata";
include "include/dotempmods";
include "include/client";
include "include/classes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_CURSE;
const SPELL_EFFECT_ID		:= FX_SPELL_CURSE;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_CURSE;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_CURSE;

program cast_curse( parms, spell_id )

	var circle := 4;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
 		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif
		send_attack( cast_on , caster , SPELLID_CURSE );
	else

        caster := parms;
		// For scrolls, StartSpellScript isn't working >:(
		if (TypeOf(parms) == "Array")
			caster := parms[1];
			spell_id := parms[2];
		endif
		if (TryToCast( caster, spell_id ) != SUCCESS);
			EraseObjProperty(caster, "#Casting");
			return 0;
		endif
        cast_on := CanTargetSpell(caster, circle, TGTOPT_HARMFUL + TGTOPT_CHECK_LOS );
        if (!cast_on)
            return;
        endif
	endif

	if( !fromhit )
		detach();
		cast_on := Reflected( caster , cast_on , circle );
	endif
		PlayObjectCenteredEffect(cast_on, 
                                         SPELL_EFFECT_ID,
                                         SPELL_EFFECT_SPEED,
                                         SPELL_EFFECT_LOOP);
		
	if( !CanMod( cast_on , "all" ) || !CanMod( cast_on , "ebless" ) )
		SendSysMessage(caster,"Already under the influence!");
		return;
	endif
	
	PlaySoundEffect(cast_on, SFX_SPELL_CURSE);
	
	var immunity := IsProtected( caster , cast_on , circle );
	if( immunity == IMMUNED )
		return;
	endif
	
	var mod_amount := 0;
	if(GetObjProperty(cast_on, CLASSEID_WARRIOR))
		mod_amount := Resisted( caster , cast_on , circle , GetModAmount( caster )*0.35);
	else
		mod_amount := Resisted( caster , cast_on , circle , GetModAmount( caster )*0.5);
	endif
	
	var duration := GetModDuration( caster );
	if( immunity == CURSED )
		SendSysmessage( cast_on , "One of your equipped items increases the effect of the spell!" );
		mod_amount := mod_amount * 2;
		duration := duration * 2;
	endif
	
	DoTempMod(cast_on, "all", 0 - mod_amount, duration);

endprogram
