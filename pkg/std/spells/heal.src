use uo;
use os;

include "include/spelldata";
include "include/damages";
include "include/random";
include "include/client";
include "include/attributes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_HEAL;
const SPELL_EFFECT_ID		:= FX_SPELL_HEAL;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_HEAL;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_HEAL;

program cast_heal( parms, spell_id )

	var circle := 1;
	var caster;
	var cast_on;
	var fromhit;


	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		// For scrolls, StartSpellScript isn't working >:(
		if (TypeOf(parms) == "Array")
			caster := parms[1];
			spell_id := parms[2];
		endif
		if (TryToCast( caster, spell_id ) != SUCCESS);
			EraseObjProperty(caster, "#Casting");
			return 0;
		endif
		cast_on := CanTargetSpell(caster, circle, TGTOPT_NEUTRAL + TGTOPT_CHECK_LOS );
		if (!cast_on)
			return;
		endif
	endif

	if( !fromhit )
		detach();
		cast_on := Reflected( caster , cast_on , circle );
	endif
		PlayObjectCenteredEffect(cast_on, 
                                         SPELL_EFFECT_ID,
                                         SPELL_EFFECT_SPEED,
                                         SPELL_EFFECT_LOOP);
		//SendSysMessage(caster, CStr(playeff));
	
	
	PlaySoundEffect( cast_on , SFX_SPELL_HEAL );
	
	var die_string := "3d2";
	var healed := RandomDiceStr( die_string ) ;
	var maxx := 20;
	if( GetObjProperty( caster , CLASSEID_MAGE ) )
		maxx := Cint( maxx * CLASSE_BONUS );
	elseif( GetObjProperty( caster , CLASSEID_CLERIC ) )
		maxx := Cint( maxx * CLASSE_BONUS );
	endif
	
	healed := ModifyWithMagicEfficiency( caster , healed + Cint( GetEffectiveSkill(caster, SKILLID_MAGERY)/20)+5 );
	if (healed > maxx)
		healed := maxx;
	endif
	
	var bonus := GetObjProperty( cast_on , "HealingIncrease" );
	if( bonus )
		healed := healed + Cint(healed * bonus * 10 / 100);
	endif
	
	if( GetObjProperty(cast_on , "Type" ) == "Undead" )
		ApplyTheDamage( cast_on , caster , healed , DMGID_MAGIC );
	else
		HealDamage( cast_on , healed );
		SendSysMessage( caster, "You healed " + healed + " damage." );
	endif

endprogram
