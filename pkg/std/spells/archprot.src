use uo;
use os;

include "include/client";
include "include/dotempmods";
include "include/spelldata";
include "include/attributes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_ARCH_PROTECTION;
const SPELL_EFFECT_ID		:= FX_SPELL_ARCH_PROTECTION;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_ARCH_PROTECTION;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_ARCH_PROTECTION;

program cast_archprot( parms, spell_id )

	var circle := 4;
	var caster;
	var thetarget;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		thetarget := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;
		// For scrolls, StartSpellScript isn't working >:(
		if (TypeOf(parms) == "Array")
			caster := parms[1];
			spell_id := parms[2];
		endif
		if (TryToCast( caster, spell_id ) != SUCCESS);
			EraseObjProperty(caster, "#Casting");
			return 0;
		endif
		thetarget := CanTargetArea(caster, circle);
		if (!thetarget)
			return;
		endif
	endif
	
	PlaySoundEffect(caster, SFX_SPELL_PROTECTION);

	var magery := GetEffectiveskill( caster , SKILLID_MAGERY );

	var mod_amount := Cint( GetModAmount( caster ) / 1.5 );
	var duration := GetModDuration( caster );
	var affected :=ListMobilesNearLocation( thetarget.x, thetarget.y, thetarget.z, cint(magery/15) );
	
	affected := SmartBoost(caster, affected);

	foreach cast_on in affected
		if (CanMod(cast_on, "ar"))
			PlayObjectCenteredEffect( cast_on, FX_SPELL_ARCH_PROTECTION , SPEED_SPELL_ARCH_PROTECTION , LOOP_SPELL_ARCH_PROTECTION );
			DoTempMod(cast_on, "ar", mod_amount, duration);
			SendBoostMessage( caster, cast_on, "arch protection", mod_amount, duration, 1 );
		endif
	endforeach

endprogram
