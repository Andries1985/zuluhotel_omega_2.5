use uo;

include "include/spelldata";
include "include/client";
include "include/objtype";
include "include/skillpoints";
include "include/bard";
include "include/eventid";
include "include/checkcity";
include "include/dotempmods";

program cast_songofcloaking( player )
	SetObjProperty( player , "#Casting", 1 );
	var singinghp := GetHp( player );
	var circle := GetCircle( SPELLID_SONG_OF_CLOAKING  );
	var instrument := findinstrument( player );
	var sfx_success;
	var sfx_failure;
	case( instrument.graphic )
		UOBJ_HARP:	sfx_success := SFX_HARP_SUCC;
				sfx_failure := SFX_HARP_FAIL;
				break;
				
		UOBJ_LUTE:	sfx_success := SFX_LUTE_SUCC;
				sfx_failure := SFX_LUTE_FAIL;
				break;
				
		UOBJ_DRUM:	sfx_success := SFX_DRUM_SUCC;
				sfx_failure := SFX_DRUM_FAIL;
				break;
				
		UOBJ_TAMB:	sfx_success := SFX_TAMB_SUCC;
				sfx_failure := SFX_TAMB_FAIL;
				break;
				
		default:	PrintTextAbovePrivate( player, "You dont have an Instrument!",player );
				EraseObjProperty( player , "#Casting" );
				return;
	endcase
	

	var peace := GetAttribute(player, ATTRIBUTEID_PEACEMAKING);
	var mus := GetAttribute(player, ATTRIBUTEID_MUSICIANSHIP);
	var spec := GetObjProperty(player,"IsBard");
	var ps;
	if (!spec)
		spec := 0;	
	endif
	var rav := Cint(GetVital(player, VITALID_STAMINA) / 100);
	var dmod;
	var duration;
	var proped := array();
	var drain := (circle*2);
	var difficulty := (circle*8)+(Random(25)-(spec*8));
	
	//PrintTextAbove(player,"Difficulty:"+difficulty);
	PerformAction( player, 0X021 );
	if (difficulty > peace or difficulty > mus or !spec)
		PlaySoundEffect( player , sfx_failure );
		EraseObjProperty( player , "#Casting" );
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		ps := GetStamina( player ) -(drain/2);
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
		return;
	elseif ((GetStamina( player ) -drain) <  0)
		PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
		PlaySoundEffect( player , sfx_failure );
		PrintTextAbovePrivate( player, "Out of Stamina!",player );
		EraseObjProperty( player , "#Casting" );
	else
		ps := GetStamina( player ) -drain;
		if (ps < 0 )
			ps := 0;
		endif
		SetStamina( player, ps );
		sleep(2);
		if (GetHp( player ) < singinghp)
			PrintTextAbove(player,"*"+player.name+" plays a song poorly*");
			PrintTextAbovePrivate( player, "You were interrupted!",player );
			PlaySoundEffect( player , sfx_failure );
			EraseObjProperty( player , "#Casting" );
			return;
		else
			PlaySoundEffect( player , sfx_success );
			PrintTextAbove(player,"Velo Ipse Attritus Iniquus");
		endif

		var bardhid := 0;
		dmod := (peace/10) + spec*2;
		duration := dmod*10;
		//sleep(1);
		var affected := ListMobilesInLineOfSight( player , cint( (spec+1) * 2 )) + player;

		affected := SmartBoost(player, affected);
		
		foreach cast_near in affected
			if(!ValidSongBoost(player, cast_near))
				continue;
			endif
		
			PlaySoundEffect(cast_near, SFX_SPELL_INVISIBILITY);	
			SetObjProperty(cast_near, "StealthCD", ReadGameClock()+10);
			cast_near.hidden := 1;
			SetObjProperty(cast_near,"cloaked",1);
			bardhid := 1;
		endforeach
	
    	EraseObjProperty( player , "#Casting" );
		
		detach();
		sleep(duration);
		foreach cast_near in affected
			if (bardhid == 1)
				cast_near.hidden := 0;
				SetObjProperty(cast_near,"cloaked",0);
			endif
		endforeach
	endif

endprogram
