use uo;

include "include/areas";
include "include/spelldata";
include ":areas:include/areafunctions";

program kegBomb(who)
	var charges := GetObjProperty(who, "bmCharges"), bomb, bmPlaced := GetObjProperty(who, "bmPlaced");

	if(!GetObjProperty( who, "bmPlayer" ))
		SendSysMessage( who, "Only Bomberman players can use this item.");
		return;
	endif

	if(!charges)
		charges := 1;
	endif

	if(!bmPlaced)
		SetObjProperty(who, "bmPlaced", 0);
		bmPlaced := GetObjProperty(who, "bmPlaced");
	endif

	if(!IsInBox(who, 5508, 5527, 1655, 1674))
		SendSysMessage(who, "Can't use that here.");
		return;
	endif

	if(bmPlaced < charges)
		bomb := CreateItemAtLocation( who.x, who.y, who.z, 0x9A92 );
		bomb.color := GetObjProperty(who, "bmColor");
		SetObjProperty(who, "bmPlaced", GetObjProperty(who, "bmPlaced")+1);
	else
		SendSysMessage(who, "Can't use anymore bombs at once.");
		return;
	endif

	Detach();
	var count := 0, bombExplod;
	While(count <= 60)
		if(count == 0)
			PrintTextAbove( bomb, "3" );
		elseif(count == 20)
			PrintTextAbove( bomb, "2" );
		elseif(count == 40)
			PrintTextAbove( bomb, "1" );
		endif
		bombExplod := GetObjProperty(bomb, "ExplodeNOW");
		if(bombExplod)
			break;
		endif
		count := count+1;
		SleepMS(50);
	endwhile
	
	shootFire(who, bomb, 1, 0);
	shootFire(who, bomb, 0, 1);
	shootFire(who, bomb, -1, 0);
	shootFire(who, bomb, 0, -1);
	DestroyItem(bomb);
	SetObjProperty(who, "bmPlaced", GetObjProperty(who, "bmPlaced")-1);

endprogram

function shootFire(who, bomb, j, h)

	var range := GetObjProperty(who, "bmRangeBoost"), victim, stones, stopFire := 0, i, life;
	if(!range)
		range := 1;
	endif
	
	PlayStationaryEffectEx( bomb.x, bomb.y, bomb.z, "britannia", FX_SPELL_FLAME_STRIKE, SPEED_SPELL_FLAME_STRIKE, LOOP_SPELL_FLAME_STRIKE, GetObjProperty(who, "bmColor") );
	victim := ListMobilesNearLocation( bomb.x, bomb.y, bomb.z, 0 );
	foreach mobile in victim
		if(GetObjProperty(mobile, "bmPlayer"))
			life := GetObjProperty(mobile, "BmLife");
			if(!GetObjProperty(mobile, "BmLifeCD") || GetObjProperty(mobile, "BmLifeCD") < ReadGameClock())
				if(!life)
					RemoveSafeAreaProperties(mobile);
					ApplyDamage(mobile, GetMaxHP(mobile)+5);
				else
					ApplyDamage(mobile, GetMaxHP(mobile)+5);
					SetObjProperty(mobile, "BmLife", GetObjProperty(mobile, "BmLife")-1);
					SetObjProperty(mobile, "BmLifeCD", ReadGameClock()+2);
				endif
			endif
		endif
	endforeach
	
	for ( i := 0; i < range; i:=i+1 )
		if( (bomb.x+j) > 5527 || (bomb.x+j) < 5508 || (bomb.y+h) > 1674 || (bomb.y+h) < 1655 )
			stopFire := 1;
		endif

		stones := ListItemsAtLocation( bomb.x+j, bomb.y+h,  bomb.z );
		foreach boulder in stones
			if(GetObjProperty(boulder, "bmBlocker"))
				stopFire := 1;
			endif
		endforeach

		if(!stopFire)
			PlayStationaryEffectEx( bomb.x+j, bomb.y+h, bomb.z, "britannia", FX_SPELL_FLAME_STRIKE, SPEED_SPELL_FLAME_STRIKE, LOOP_SPELL_FLAME_STRIKE, GetObjProperty(who, "bmColor") );
			victim := ListMobilesNearLocation( bomb.x+j, bomb.y+h, bomb.z, 0 );
			foreach mobile in victim
				if(GetObjProperty(mobile, "bmPlayer"))
					life := GetObjProperty(mobile, "BmLife");
					if(!GetObjProperty(mobile, "BmLifeCD") || GetObjProperty(mobile, "BmLifeCD") < ReadGameClock())
						if(!life)
							RemoveSafeAreaProperties(mobile);
							ApplyDamage(mobile, GetMaxHP(mobile)+5);
						else
							ApplyDamage(mobile, GetMaxHP(mobile)+5);
							SetObjProperty(mobile, "BmLife", GetObjProperty(mobile, "BmLife")-1);
							SetObjProperty(mobile, "BmLifeCD", ReadGameClock()+2);
						endif
					endif
				endif
			endforeach
		
			stones := ListItemsAtLocation( bomb.x+j, bomb.y+h,  bomb.z );
			foreach boulder in stones
				if(GetObjProperty(boulder, "bmStone"))
					powerUp(boulder);
					DestroyItem(boulder);
					stopFire := 1;
				elseif(GetObjProperty(boulder, "bmBomb"))
					SetObjProperty(boulder, "ExplodeNOW", 1);
				elseif(GetObjProperty(boulder, "bmBoost"))
					DestroyItem(boulder);
				endif
			endforeach

			if(j<0)
				j := j-1;
			elseif(j>0)
				j := j+1;
			endif
			if(h<0)
				h := h-1;
			elseif(h>0)
				h := h+1;
			endif
		endif
	endfor
endfunction

function powerUp(boulder)
	var chance := Random(100)+1;
	if(chance > 75)
		chance := Random(1000)+1;
		if( chance < 300)
			CreateItemAtLocation( boulder.x, boulder.y, boulder.z, 0x9A96 ); //Range
		elseif( chance < 600)
			CreateItemAtLocation( boulder.x, boulder.y, boulder.z, 0x9A97 ); //Charges
		elseif( chance < 850)
			CreateItemAtLocation( boulder.x, boulder.y, boulder.z, 0x9A98 ); //Kick
		else
			CreateItemAtLocation( boulder.x, boulder.y, boulder.z, 0x9A99 ); //Speed
		endif
	endif
endfunction