use uo;
use os;

include "include/random";
include "include/spelldata";
include "include/dotempmods";
include "include/classes";
include "include/client";
include "include/attributes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_WRAITHFORM;
const SPELL_EFFECT_ID		:= WRAITHFORM_MORPH_EFFECT;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_WRAITHFORM;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_WRAITHFORM;

program cast_wraithform( parms )

	set_script_option( SCRIPTOPT_CAN_ACCESS_OFFLINE_MOBILES , 1 );
	
	var circle := GetCircle( SPELLID_WRAITHFORM );
	var plane := NECRO;
	var caster;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		if (parms[4])
			circle := parms[4];
		endif

	else
		caster := parms;	
      
		if( TryToCast( caster, SPELLID_WRAITHFORM ) != SUCCESS )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
	endif

	if( !CanMod(caster, "poly") )
		SendSysMessage( caster , "You're already polymorphed in something." );
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
	
	var oldCharGraphic	:= caster.graphic;
	var oldCharColor	:= caster.color;
	var casterSkill	:= GetEffectiveSkill( caster , SKILLID_MAGERY );
	var duration		:= ModifyWithMagicEfficiency( caster , Cint( casterSkill / 15 ) );
	var range		:= ModifyWithMagicEfficiency( caster , Cint( casterSkill / 30 ) );
	var powerlvl		:= ModifyWithMagicEfficiency( caster , Cint( casterSkill / 15 ) );
	var sleeptime		:= 5;

	sleeptime	:= Cint( sleeptime / ClasseBonus( caster , CLASSEID_MAGE ) );
	
	var damageDie		:= "2d" + powerlvl;

	caster.color		:= WRAITHFORM_WRAITH_COLOR;
	
	PlaySoundEffect( caster , WRAITHFORM_MORPH_SOUND );
	
	EraseObjProperty( caster , "#Casting" );
	PolymorphMobile( caster , WRAITHFORM_WRAITH_GRAPHIC , WRAITHFORM_WRAITH_GRAPHIC , duration , 1 );
	SendBoostMessage( caster, caster, "wraithform", 1, duration, 0 );
	
	while( duration > 0 and !caster.dead )
	
		var victims := ListMobilesNearLocation( cint(caster.x) , cint(caster.y) , cint(caster.z) , cint(range) );

		victims := SmartAoE(caster, victims);

		foreach mobile in victims
			if( ( mobile.serial != caster.serial ) and ( mobile.dead == 0 ) )
				if (!mobile.multi.components)
					var protect := IsProtected( caster , mobile , circle );
					if( protect != IMMUNED )
						var dmg := RandomDiceStr( damageDie );
						if( protect == CURSED )
							dmg := dmg * 2;
						endif
					
						PlayObjectCenteredEffect( mobile , WRAITHFORM_DRAIN_EFFECT , 7 , 0x10 );
						PlaySoundEffect( mobile , WRAITHFORM_DRAIN_SOUND );
						ApplyPlanarDamage( caster , mobile , circle , dmg , plane );
						SetMana(caster, GetMana(caster)+dmg);//caster.mana := caster.mana + dmg;
						if(  GetMana(caster) >  GetMaxMana(caster) )
							SetMana(caster,GetMaxMana(caster));
						endif
					endif
				endif
			endif
		endforeach

		duration := duration - 1;
		Sleep( sleeptime );
		
	endwhile

	if( caster.dead == 1 )
		oldCharGraphic := oldCharGraphic + 2;
	endif
	
	caster.graphic	:= oldCharGraphic;
	caster.color	:= oldCharColor;
	PlayObjectCenteredEffect( caster , WRAITHFORM_MORPH_EFFECT , SPELL_EFFECT_SPEED , SPELL_EFFECT_LOOP );
	PlaySoundEffect( caster , WRAITHFORM_MORPH_SOUND );

endprogram
