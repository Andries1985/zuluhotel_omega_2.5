use uo;


include "include/spelldata";
include "include/dotempmods";
include "include/client";
include "include/attributes";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_DECAYING_RAY;
const SPELL_EFFECT_ID		:= FX_SPELL_DECAYING_RAY;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_DECAYING_RAY;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_DECAYING_RAY;

program decayingray( parms )

	var circle := GetCircle( SPELLID_DECAYING_RAY ) + 1;
	var plane := NECRO;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
        if(IsInSafeArea(caster))
            return;
        endif
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;	
        if(IsInSafeArea(caster))
			EraseObjProperty( caster , "#Casting" );
            SendSysMessage(caster, "This spell can not be cast here.");
            return;
        endif
		if( TryToCast( caster, SPELLID_DECAYING_RAY ) != SUCCESS )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
		SendSysmessage(caster, "Select a target.");
		cast_on := CanTargetSpell ( caster , circle , TGTOPT_CHECK_LOS + TGTOPT_HARMFUL );			
	endif
	
	if( !cast_on )
		EraseObjProperty( caster , "#Casting" );
		return;
	endif	

	if( !fromhit )
		detach();
		cast_on := Reflected( caster , cast_on , circle );
	endif
	
        PlaySoundEffect(caster, SFX_SPELL_DECAYING_RAY );
		//PlayObjectCenteredEffect(cast_on, SPELL_EFFECT_ID, SPELL_EFFECT_SPEED, SPELL_EFFECT_LOOP);
		PlayObjectCenteredEffectEx( cast_on, SPELL_EFFECT_ID, SPELL_EFFECT_SPEED, SPELL_EFFECT_LOOP );

	var immunity := IsProtected( caster , cast_on , circle );
	if( immunity == IMMUNED )
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
	
	if( !CanMod( cast_on , "ar" ) )
		SendSysmessage( caster , "The target have a spell on him that protect him from the spell effect." );
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
	
	
	var amount := Cint( GetModAmount( caster ) * 1.25 );
	var duration := GetModDuration( caster );
	var protection := GetResultingPlaneProtection( cast_on , plane );
	
	if( protection )
		var modifier := 100 - (protection * 20);
		if( modifier < 1 )
			modifier := 0;
		endif
			
		amount := Cint( amount * modifier / 100 );
		duration := Cint( duration * modifier / 100 );

		if( !duration or !amount )
			PrintTextAbovePrivate( cast_on , "The nature of the target prevent it from being affected by that spell!" , caster );
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
	endif

	amount := Resisted( caster , cast_on , circle , amount );
	duration := Resisted( caster , cast_on , circle , duration );
	
	if( !amount )
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
	
        DotempMod( cast_on , "ar" , 0 - amount , duration );
        SendSysMessage( caster , "You reduced " + cast_on.name + "'s armor rating by " + amount + "." );
        SendSysmessage( cast_on , "Your armor rating was reduced by " + amount + "." );
        EraseObjProperty( caster , "#Casting" );

endprogram
