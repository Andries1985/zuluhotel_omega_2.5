use uo;
use os;


include "include/spelldata";
include "include/classes";
include "include/client";
include "include/attributes";
include "include/managers";

const SPELL_EFFECT_TYPE_MOVING 	:= MOVING_SPELL_CONTROL_UNDEAD;
const SPELL_EFFECT_ID		:= FX_SPELL_CONTROL_UNDEAD;
const SPELL_EFFECT_SPEED	:= SPEED_SPELL_CONTROL_UNDEAD;
const SPELL_EFFECT_LOOP		:= LOOP_SPELL_CONTROL_UNDEAD;

program cast_controlundead( parms )
	
	var circle := GetCircle( SPELLID_CONTROL_UNDEAD );
	var plane := NECRO;
	var caster;
	var cast_on;
	var fromhit;

	if (parms[1] == "#MOB")
		caster := parms[2];
		cast_on := parms[3];
		if (parms[4])
			circle := parms[4];
		endif
		if (parms[5])
			fromhit := parms[5];
		endif

	else
		caster := parms;	
		if( TryToCast( caster, SPELLID_CONTROL_UNDEAD ) != SUCCESS )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
		SendSysmessage( caster , "Select a target." );
		cast_on := CanTargetSpell( caster , circle , TGTOPT_CHECK_LOS + TGTOPT_HARMFUL );
		if( !cast_on )
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
	endif
	
	if( !fromhit )
		EraseObjProperty( caster , "#Casting" );
		cast_on := Reflected( caster , cast_on , circle );
	else
		PlayObjectCenteredEffect(cast_on, 
                                         SPELL_EFFECT_ID,
                                         SPELL_EFFECT_SPEED,
                                         SPELL_EFFECT_LOOP);
	endif
	
	if( GetObjProperty( cast_on , "Type" ) != "Undead" )
		SendSysmessage( caster , "That isn't an undead!" );
		EraseObjProperty( caster , "#Casting" );
		return;
	endif
	
	if( GetObjProperty( cast_on , "untamable" ) )
		SendSysmessage( caster , "You can't take the control of this creature." );
		EraseObjProperty( caster , "#Casting" );
		return;
	endif

	var conf	:= ReadConfigFile ("::npcdesc");
	var elem	:= FindConfigElem(conf, cast_on.npctemplate);
	if (!elem)
		SendSysmessage(caster, "You cannot take the control of this!");
		EraseObjProperty( caster , "#Casting" );
		return;
	endif

	if( (elem.Privs == "invul") || GetObjProperty( cast_on, "Boss" ) || GetObjProperty( cast_on, "SuperBoss"))
	  	SendSysmessage(caster, "You can't take the control of that!");
	  	EraseObjProperty( caster , "#Casting" );
		return;
	endif

	PlaySoundEffect( caster , SFX_SPELL_CONTROL_UNDEAD );
	
	var protected := IsProtected( caster , cast_on , circle );
	if( protected == IMMUNED )
		EraseObjProperty( caster , "#Casting" );
		return;
	endif

	var duration := ModifyWithMagicEfficiency( caster , GetEffectiveSkill( caster , SKILLID_MAGERY ) * 3 );
	var protect := GetResultingPlaneProtection( cast_on , plane );
	if( protect )
		duration := duration - ( duration * protect * 0.25 );
		if( duration < 1 )
			SendSysmessage( caster , "This undead is immuned to your spell!" );
			EraseObjProperty( caster , "#Casting" );
			return;
		endif
	endif
	
	var oldscript := cast_on.script;
	SendSysmessage( caster , "Control Undead is successful!" );
	SetObjProperty( cast_on , "master" , caster.serial );
	EraseObjProperty( cast_on, "SpawnPoint" );
	EraseObjProperty( cast_on, "Anchor" );
	SetObjProperty( cast_on, "charmed", 1 );
	PrintTextAbove( cast_on, "You take control of " + cast_on.name );
	WarnManagerOfNpcDeath(cast_on);
	cast_on.script := "tamed";
	RestartScript( cast_on );
	
	EraseObjProperty( caster , "#Casting" );
	Sleep( duration );
	
	cast_on.script := oldscript;
	RestartScript( cast_on );
	EraseObjProperty( cast_on , "master" );	

endprogram
