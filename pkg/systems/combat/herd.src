// herd.src was created by Maximilian Scherr
use uo;
use cfgfile;
use attributes;

include "include/attributes";
include "include/client";

program herd(who)
if( ReadGameClock() < Cint(GetObjProperty( who , "#lastherd" ) + 10) )
	SendSysMessage( who , "You must wait before trying to herd again." );
	return;
endif
  SendSysMessage(who, "What do you want to herd?");
  var npc := Target(who);
  if (!npc)
    SendSysMessage(who, "Cancelled!");
    return;
  endif
  if (!CheckLineOfSight(who, npc))
    SendSysMessage(who, "You can't see that!");
  endif
  SetObjProperty( who , "#lastherd" , ReadGameClock() );	
  var conf := ReadConfigFile("::npcdesc");
  var elem := FindConfigElem(conf, npc.npctemplate);
  var tameskill := CInt(GetConfigInt(elem, "tameskill"));
  if (!tameskill)
    SendSysMessage(who, "You can't herd that!");
    return;
  endif
  if ((npc.script == "tamed")  or (npc.script == "totem"))
    SendSysMessage(who, "You can't herd that!");
    return;
  endif
  if (npc.script == ":herding:herdedai")
    SendSysMessage(who, "That is already being herded.");
    return;
  endif
  var difficulty := CInt(tameskill);
	SendSysmessage( who , "Difficulty: "+difficulty );
  var points := CInt(difficulty) * 5;
  if (CheckSkill(who, SKILLID_HERDING, CInt(difficulty), CInt(points)))
    SendSysMessage(who, "Where do you want to make "+ npc.name +" run to?");
    var location := TargetCoordinates(who);
    if (!CheckLosAt(npc, location.x, location.y, location.z))
      SendSysMessage(who, npc.name +" can't see that location!");
      return;
    endif
    SetObjProperty(npc, "locationx", location.x);
    SetObjProperty(npc, "locationy", location.y);
    SetObjProperty(npc, "originalscript", npc.script);
    npc.script := ":herding:herdedai";
    RestartScript(npc);
    PrintTextAbove(npc, "*"+ npc.name +" is herded by "+ who.name +"*");
  else
    SendSysMessage(who, "You fail to herd the creature.");
    return;
  endif
endprogram
