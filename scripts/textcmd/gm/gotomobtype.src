//Edited August 25, 2024

use uo;
use os;

include ":staff:include/staff";
include ":gumps:include/gumps";

program textcmd_gotomobtype( character , text )

	text := SplitWords(text);

	var template := Lower(text[1]);
	var realm := Lower(text[2]);

	if (!realm)
		SendSysMessage( character, "Usage is .gotosmobtype <NPCTemplate> <realm>" );
		return 0;
	elseif ( !template )
		SendSysMessage( character, "Usage is .gotosmobtype <NPCTemplate> <realm>" );
		return 0;
	endif

	if (realm == "felucca")
		realm := "britannia";
	elseif (realm == "trammel")
		realm := "britannia_alt";
	endif

	//check for mobs
	var found := {};

	var realmData := Realms(realm);
    if (!realmData)
        SendSysMessage(character, $"Can't find realm: {realm}");
        return 0;
    endif

    foreach mobile in ListMobilesInBox(0, 0, -127, realmData.width, realmData.height, 128, realm);
        if( mobile.IsA(POLCLASS_NPC) )
            if (mobile.NPCTemplate == template )
                found.append(mobile);
                SleepMs(2);
            endif
        endif
    endforeach

	//Once found the mobs wanted, send to gump to list
	var chr := MakeAndProcessGump( character, found, realm );
	if( chr )
        LogCommand(character, GetProcess(GetPid()).name, chr.name+"/"+chr.serial, "XYZ - "+chr.x+","+chr.y+","+chr.z);
        var moved := MoveObjecttoLocation( character , chr.x , chr.y , chr.z , chr.realm, MOVEOBJECT_FORCELOCATION );
              if (moved.errortext)
               SendSysMessage(character, "oh no an error... can't go to: " + moved.errortext);
              endif
    endif

endprogram

function MakeAndProcessGump( character, found, realm )

	var realmname;

	if (realm == "britannia")
		realmname := "Felucca";
	elseif (realm == "britannia_alt")
		realmname := "Trammel";
	elseif (realm == "malas")
		realmname := "Malas";
	elseif (realm == "tokuno")
		realmname := "Tokuno";
	elseif (realm == "ilshenar")
		realmname := "Ilshenar";
	else
		realmname := "Error";
	endif

	var gump := GFCreateGump(20, 20, 500, 720);
	GFDisposable(gump, 0);
	var y := 185;
	var perpage := 20;
	//var totalpages := ((len(found))/20); 
	var page := 1;
	var i := 0;
	var start := 1;

	// The main gump screen
	GFResizePic(gump, 20, 20, 9250, 500, 725);

	// The area background gumps
	GFResizePic(gump, 38, 180, 9200, 464, 528);
	GFResizePic(gump, 38, 140, 9200, 464, 40);

	// Title text
	GFResizePic(gump, 175, 35, 9200, 155, 20);
	GFTextLine(gump, 180, 35, 67, "Found NPCs in " +realmname);

	// Change Info Buttons
	GFTextLine(gump, 110, 70, 57, "Dummy Button 1");
	GFAddButton(gump, 60, 70, 4005, 4006, GF_DUMMY_BTN, 0); 
	GFTextLine(gump, 110, 105, 57, "Dummy Button 2");
	GFAddButton(gump, 60, 105, 4005, 4006, GF_DUMMY_BTN, 0); 

	// Column Titles
	GFTextLine(gump, 96, 150, 900, "Mob Name");
	GFTextLine(gump, 300, 150, 900, "Location");
	GFTextLine(gump, 450, 150, 900, "Goto");

	GFTextLine(gump, 240, 708, 1000, "Page 1");
	GFPage(gump, 1);
	//Fill the gump
	for( i := start ; i <= found.size() ; i := i + 1 )
		
		if (perpage <= 0)
			GFAddButton(gump, 450, 705, 4005, 4006, 0, page+1); // Next Page button
			GFPage(gump, page + 1);
			GFAddButton(gump, 50, 705, 4014, 4015, 0, page); // Prev Page button
			page := page + 1;
			GFTextLine(gump, 240, 708, 1000, "Page "+page);
			y := 185;
			perpage := 20;
		endif

		GFTextLine(gump, 96, y, 0, found[i].name);
		GFTextLine(gump, 300, y, 0, found[i].x + " , " + found[i].y);
		GFAddButton(gump, 450, y, 5540, 5541, GF_CLOSE_BTN, i);
		perpage := perpage - 1;
		y := y + 25;
	endfor

	var result := GFSendGump(character, gump);
	result := Cint(result[0]);

	return found[result];
	
endfunction
