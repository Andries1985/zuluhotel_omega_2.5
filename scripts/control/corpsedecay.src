use uo;
use os;
use util;
use basic;
use cfgfile;

include "include/constants/propids";

const DECAY_TIME_NPC_CORPSE		:= 30;		// In minutes.
const DECAY_TIME_PLAYER_CORPSE		:= 30;		// In minutes.


program corpse_decay( corpse )

	corpse.decayat := 0;
	Sleep(5);
	if( GetObjProperty( corpse, "npctemplate" ) )
		ProcessNpcCorpseDecaying(corpse);
	else
		ProcessHumanCorpseDecaying(corpse);
	endif
	
endprogram


function ProcessHumanCorpseDecaying( corpse )

	Sleep( Cint(DECAY_TIME_PLAYER_CORPSE * 60 / 2) );

	var name;
	var usescript;
	if( !GetObjProperty( corpse, "NoDecay" ) )
		corpse.color	:= 0;
		corpse.graphic	:= 0xeca + RandomInt(9);
		name := corpse.name;
		name["A corpse"] := "Remains";
		SetName( corpse, name );
		EraseObjProperty( corpse, PROPID_CORPSE_IS_BLUE );
		Sleep( Cint(DECAY_TIME_PLAYER_CORPSE * 60 / 2) );
		if( !GetObjProperty( corpse, "NoDecay" ) )
			foreach item in EnumerateItemsInContainer(corpse)
				if( item.container == corpse )
					MoveObjectToLocation( item, corpse.x, corpse.y, corpse.z, corpse.realm, MOVEOBJECT_NORMAL );
					if(corpse.multi)
						item.decayat := 0;
					endif
				endif
				usescript:=GetObjProperty( item, "old_us");
   				if(usescript)
   					eraseObjProperty( item, "old_us");
   					item.usescript := cstr(usescript);
   				else
   					item.usescript := "";
   				endif
			endforeach

			DestroyItem(corpse);
		endif
	else
		foreach item in EnumerateItemsInContainer(corpse)
			usescript:=GetObjProperty( item, "old_us");
			if(usescript)
				eraseObjProperty( item, "old_us");
   				item.usescript := cstr(usescript);
   			else
   				item.usescript := "";
   			endif
		endforeach
	endif

endfunction


function ProcessNpcCorpseDecaying( corpse )

	Sleep( Cint(DECAY_TIME_NPC_CORPSE*60) );
	DestroyItem(corpse);

endfunction
